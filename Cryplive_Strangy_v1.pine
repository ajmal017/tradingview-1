// trinhminhtriet
// https://triettrinh.com
//@version=4
// ADI DI
// RSI Divergence
study("Cryplive_Strangy_v1")

adxlen = input(9, title="ADX Smoothing")
dilen = input(14, title="DI Length")
ADX_Entry = input(25, title="ADX Entry")

//ADX
dirmov(len) =>
	up = change(high)
	down = -change(low)
	truerange = rma(tr, len)
	plus = fixnan(100 * rma(up > down and up > 0 ? up : 0, len) / truerange)
	minus = fixnan(100 * rma(down > up and down > 0 ? down : 0, len) / truerange)
	[plus, minus]

adx(dilen, adxlen) => 
	[plus, minus] = dirmov(dilen)
	sum = plus + minus
	adx = 100 * rma(abs(plus - minus) / (sum == 0 ? 1 : sum), adxlen)
	[adx, plus, minus]

[sig, up, down] = adx(dilen, adxlen)

// plot(ADX_Entry, color=color.white, linewidth=1)
adx_color = down-up > 0 ? color.red : color.lime
// bgcolor(adx_color, transp=90)
plot(sig, title="ADX", color=adx_color, linewidth=1)
plot(sig, title="ADX", color=adx_color, style=plot.style_columns, linewidth = 2, transp=90)
plot(up, color=color.green, linewidth=1, title="DI+", transp=60)
plot(down, color=color.red, linewidth=1, title="DI-", transp=60)
plot(cross(up,down) ? down : na, color=adx_color, style=plot.style_circles, linewidth=3, title="Cross DI") 


//RSI Divergence
rsi_src_fast = close, rsi_len_fast = input(5, minval=1, title="Length Fast RSI")
rsi_src_slow = close, rsi_len_slow = input(14,minval=1, title="Length Slow RSI")
rsi_up_fast = rma(max(change(rsi_src_fast), 0), rsi_len_fast)
rsi_down_fast = rma(-min(change(rsi_src_fast), 0), rsi_len_fast)
rsi_fast = rsi_down_fast == 0 ? 100 : rsi_up_fast == 0 ? 0 : 100 - (100 / (1 + rsi_up_fast / rsi_down_fast))
rsi_up_slow = rma(max(change(rsi_src_slow), 0), rsi_len_slow)
rsi_down_slow = rma(-min(change(rsi_src_slow), 0), rsi_len_slow)
rsi_slow = rsi_down_slow == 0 ? 100 : rsi_up_slow == 0 ? 0 : 100 - (100 / (1 + rsi_up_slow / rsi_down_slow))
plot_rsi_fast = plot(rsi_fast, title="Fast RSI", color=color.blue, linewidth = 1, transp=80)
plot_rsi_slow = plot(rsi_slow, title="Slow RSI", color=color.orange, linewidth = 1, transp=10)
rsi_divergence = rsi_fast - rsi_slow
plotdiv = plot(rsi_divergence, title="RSI Divergence", color = rsi_divergence > 0 ? color.lime:color.red, linewidth = 2)
// band1 = hline(70,color=green)
// band0 = hline(30,color=red)
band = hline(0)

plotarrow(down-up < 0 and rsi_divergence > 0 ? 1 : na, title="LONG", colorup=color.lime, maxheight=30, minheight=10, transp=0)
plotarrow(down-up > 0 and rsi_divergence < 0 ? -1 : na, title="SHORT", colordown=color.red, maxheight=30, minheight=10, transp=0)